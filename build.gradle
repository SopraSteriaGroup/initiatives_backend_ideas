buildscript {
	ext {
		kotlinVersion = '1.1.2'
		springBootVersion = '2.0.0.BUILD-SNAPSHOT'
		jacksonVersion = '2.8.8'
		reactorVersion = '3.1.0.BUILD-SNAPSHOT'
		reactorKotlinVersion = '1.0.0.M2'
		restAssuredVersion = '3.0.3'
        asciidoctorVersion = '1.5.3'
        springRestDocsVersion = '1.2.1.RELEASE'
        ghPublishVersion = '0.2.1'
	}
	repositories {
        jcenter()
		mavenCentral()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
		classpath("org.jetbrains.kotlin:kotlin-noarg:${kotlinVersion}")
		classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
		classpath("org.jetbrains.kotlin:kotlin-test-junit:${kotlinVersion}")
        classpath("org.asciidoctor:asciidoctor-gradle-plugin:${asciidoctorVersion}")
        classpath("org.ajoberstar:gradle-git-publish:${ghPublishVersion}")
		classpath("se.transmode.gradle:gradle-docker:1.2")
	}
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-noarg'
apply plugin: 'kotlin-spring'
apply plugin: 'docker'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'org.ajoberstar.git-publish'

version = '0.0.1-SNAPSHOT'
group = "ssg"
sourceCompatibility = 1.8

ext {
    snippetsDir = file('build/generated-snippets')
}

ext['spring-restdocs.version'] = '1.2.1.RELEASE'

compileKotlin {
	kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
	kotlinOptions.jvmTarget = "1.8"
}

docker {
	baseImage "openjdk:8-jre"
	maintainer 'Jocelyn NTAKPE <https://github.com/jntakpe>'
}

repositories {
    jcenter()
	mavenCentral()
	maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-data-mongodb-reactive')
	compile('org.springframework.boot:spring-boot-starter-webflux')
	compile("io.projectreactor:reactor-kotlin-extensions:${reactorKotlinVersion}")
	compile("com.fasterxml.jackson.module:jackson-module-kotlin:${jacksonVersion}")
	compile("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}")
	compile("org.jetbrains.kotlin:kotlin-stdlib-jre8:${kotlinVersion}")
	compile("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile("io.projectreactor.addons:reactor-test:${reactorVersion}")
	testCompile("io.rest-assured:rest-assured:${restAssuredVersion}")
	testCompile("org.springframework.restdocs:spring-restdocs-restassured:${springRestDocsVersion}")
}

noArg {
	annotation("org.springframework.data.mongodb.core.mapping.Document")
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    dependsOn test
    sourceDir = file('src/docs/asciidoc')
    inputs.dir snippetsDir
    outputDir "$projectDir/src/main/resources/static"
	attributes 'snippets': snippetsDir
}

bootJar {
    dependsOn asciidoctor
    baseName = 'initiatives_backend_ideas'
    version =  version
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/html5'
    }
}

task cleanTempDir(type: Delete) {
	delete asciidoctor.outputDir
}

clean {
	dependsOn cleanTempDir
}

gitPublish {
    repoUri = 'git@github.com:SopraSteriaGroup/initiatives_backend_ideas.git'
    branch = 'gh-pages'
    repoDir = file("$buildDir/gitPublish")
    contents {
        from(file("${asciidoctor.outputDir}/html5"))
    }
    commitMessage = 'Publishing a new API guide'
}

task publish(type: GradleBuild) {
    dependsOn asciidoctor
    tasks = ['gitPublishPush']
}


task buildDocker(type: Docker) {
	setEnvironment('SPRING_PROFILES_ACTIVE','docker')
	volume("/tmp")
	addFile("build/libs/initiatives_backend_ideas-${version}.jar",'app.jar')
	runCommand("bash -c 'touch /app.jar'")
	entryPoint(['java', '-Djava.security.egd=file:/dev/./urandom', '-jar', '/app.jar'])
	exposePort 8080
}

buildDocker.dependsOn(build)